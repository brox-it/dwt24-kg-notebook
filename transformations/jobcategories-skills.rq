PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>

PREFIX o: <https://ontologies.brox.de/dwt24/>
PREFIX d: <https://data.brox.de/dwt24/>

CONSTRUCT {

  ?iri_PoolMember
    a foaf:Person ;
    rdfs:label ?label_PoolMember ;
    dcterms:identifier ?competencePoolMemberId .

  ?iri_Competence
    a o:Skill ;
    rdfs:label ?label_Competence ;
    dcterms:identifier ?competenceId ;
    dcterms:title ?competenceName .

  ?iri_JobTag
    a o:JobCategory ;
    rdfs:label ?label_JobTag ;
    dcterms:identifier ?jobTagId ;
    dcterms:title ?jobTagName .

}

WHERE {

  SERVICE <x-sparql-anything:> {
    fx:properties 
      fx:blank-nodes true ;
      fx:trim-strings true ;
      fx:null-string "" .

	  # GroupJobTags
	  ?root
		a xyz:GroupJobTags , fx:root ;
		fx:anySlot ?jobTag .
	  
	  # JobTag
	  ?jobTag
		a xyz:JobTag ;
		
		# JobTag → Id
		fx:anySlot [ a xyz:Id ; fx:anySlot ?jobTagId ] .
		
	  # JobTag → Name
	  OPTIONAL { ?jobTag fx:anySlot [ a xyz:Name ; fx:anySlot ?jobTagName ] . }
	  
	  # JobTag → GroupCompetences
	  OPTIONAL { 
		?jobTag fx:anySlot [ a xyz:GroupCompetences ; fx:anySlot ?competence ] . 
		
		# JobTag → GroupCompetences → Competence
		?competence a xyz:Competence .
		  
		# JobTag → GroupCompetences → Competence → CompetenceId
		OPTIONAL { ?competence fx:anySlot [ a xyz:CompetenceId ; fx:anySlot ?competenceId ] . }
		  
		# JobTag → GroupCompetences → Competence → CompetenceName
		OPTIONAL { ?competence fx:anySlot [ a xyz:CompetenceName ; fx:anySlot ?competenceName ] . }

		# JobTag → GroupCompetences → Competence → PoolMemberId
		OPTIONAL { ?competence fx:anySlot [ a xyz:PoolMemberId ; fx:anySlot ?competencePoolMemberId ] . }
	  }

	  # IRIs
	  BIND( IRI(CONCAT( STR(d:), "JobCategory_", ENCODE_FOR_URI(?jobTagId) )) AS ?iri_JobTag ) .
	  BIND( IRI(CONCAT( STR(d:), "Person_", ENCODE_FOR_URI(?competencePoolMemberId) )) AS ?iri_PoolMember ) .
	  BIND( IRI(CONCAT( STR(d:), "Skill_", ENCODE_FOR_URI(?competenceId) )) AS ?iri_Competence ) .

	  # Labels
	  BIND( CONCAT( "JobCategory", " ", ?jobTagId ) AS ?label_JobTag ) .
	  BIND( CONCAT( "Person", " ", ?competencePoolMemberId ) AS ?label_PoolMember ) .
	  BIND( CONCAT( "Skill", " ", ?competenceId ) AS ?label_Competence ) .

  }

}
